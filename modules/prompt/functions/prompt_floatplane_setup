#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_floatplane_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_floatplane_colors[1]}[%b%f %u%c${_prompt_floatplane_colors[4]}?%f${_prompt_floatplane_colors[1]}]%f "
  else
    branch_format="${_prompt_floatplane_colors[1]}[%b%f %u%c${_prompt_floatplane_colors[1]}]%f "
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'
}

function prompt_floatplane_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_floatplane_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_floatplane_colors=(
      "%F{5}"  # Purple - branch name
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{142}" # Yellow
      "%F{28}" # green
    )
  else
    _prompt_floatplane_colors=(
      "%F{purple}"
      "%F{red}"
      "%F{magenta}"
      "%F{yellow}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_floatplane_colors[1]}%b%f %u%c)"
  local action_format="(${_prompt_floatplane_colors[5]}%a%f)"
  local unstaged_format="${_prompt_floatplane_colors[2]}●%f"
  local staged_format="${_prompt_floatplane_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  PROMPT="
%n@%B%m%b: %~ "'${vcs_info_msg_0_}'"$ "
  RPROMPT=''
}

prompt_floatplane_setup "$@"
